plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'kr.hvy'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('springCloudVersion', "2024.0.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// 기본 환경을 local로 설정 (커맨드라인에서 -Penv=prod로 변경 가능)
def env = project.findProperty("env") ?: "local"

dependencies {

    // Library
    if (env == "prod") {
        implementation('io.github.motolies:hvy-common:0.1.13') {
            exclude group: 'org.springframework.cloud', module: 'spring-cloud-stream'
            exclude group: 'org.springframework.cloud', module: 'spring-cloud-stream-binder-kafka'
        }
    } else {
        // MavenLocal에 배포된 hvy-common을 사용
        implementation('io.github.motolies:hvy-common:local') {
            exclude group: 'org.springframework.cloud', module: 'spring-cloud-stream'
            exclude group: 'org.springframework.cloud', module: 'spring-cloud-stream-binder-kafka'
        }
    }

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'

    // jdbc driver
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.1'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    // shedlock
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:6.3.0'


    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.redisson:redisson-spring-boot-starter:3.45.0'

    // cache
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // logging
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // testContainer
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'

    // H2 Database(나중엔 테스트만 남겨야 함)
    runtimeOnly 'com.h2database:h2'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // mapstruct
    implementation 'org.mapstruct:mapstruct:1.6.2'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2' // mapstruct가 lombok 뒤에 있어야 함?
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.security:spring-security-test'

}

tasks.named('test') {
    useJUnitPlatform()
}
